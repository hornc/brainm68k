# m68kd.bf Decompiler
# 'bfc' produced Motorolla 68000 AmigaOS 2.0 executable back to bf code.

+++++[>++++++++++<-]>
[->,<]  # Skip 50 byte header
>  # one cell left for tmp
,++++++++++++++
[--------------  # Exit on 0xF2
  [  # If b != 0
    <-[>-<---]>+++++++  # neg N (78)
    >+<  # set match flag
    [   # not 78
       ----  # neg 82
       [   # not 82
           -  # neg 83
           [   # not 83
               -------------  # neg ` (96)
               [   # not 96
                   ------   # neg f (102)
                   [ # not 102
                      <----[>---<----]>+  # 34
                      [  # not "

                      >-<[-]]
                      >
                      [   # match 34
                          ,,,,,,,,,,,,
                      ]
                   >[-]<[-]]
                   >
                   [   # match 102
                       -<,,,[-]
                       <-[>+<---]>++++++++.[-]

                   ]                    

               >[-]<[-]]
               >
               [   # match 96
                  -<,,,[-]
                  <-[>+<---]>++++++.[-]

               ]


           >[-]<[-]]
           >
           [   # match 83
             -<
             ,
             <+++[>-------<-]>>+<
             [<-[>-<+++++++++]>---.[-]>[-]<]   # shift left
             >
             [[-]<<[-]--[>+<++++++]>++.[-]]   # minus

           ]        
       >[-]<[-]]
       >
       [   # match 82
         -<
         ,
         <+++[>-------<-]>>+<
         [<-[>-<+++++++++]>-.[-]>[-]<]   # shift right
         >
         [[-]<<[-]--[>+<++++++]>.[-]]   # plus

       ]
    >[-]<[-]]
    >
    [  # match 78
      -<
      ,,,
      <-[>+<-----]>+++  # neg 202
      [[-]>++<]         #  2
      --[>+<++++++]>+.  # 44
      ,,,,,,,,[-]<
    ]

  ]
,++++++++++++++]
